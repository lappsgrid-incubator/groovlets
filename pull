import javax.crypto.Mac
import javax.crypto.spec.SecretKeySpec
import java.security.InvalidKeyException
import java.security.MessageDigest

import groovy.json.*

String method = request.method

void log(String message) {
	String timestamp = new Date().format('yyyy-MM-dd hh:mm:ss')
	new File('/var/log/groovlets.log').append("[$timestamp] $message\n")
}

String sign(String msg) {
	String secret = System.env['GITHUB_SECRET']
	if (!secret) {
		return "Secret unavailable"
	}
	try {
		SecretKeySpec spec = new SecretKeySpec(secret.bytes, 'HmacSHA1')
		Mac mac = Mac.getInstance('HmacSHA1')
		mac.init(spec)
		byte[] digest = mac.doFinal(msg.bytes)
		//return asHex(digest)
		return "sha1=" + digest.encodeHex()
	} 
	catch (InvalidKeyException e) {
		return "Invalid Key: ${e.message}"
	}
}

if (method != 'POST') {
	log("Invalid request method: $method")
	response.status = 405 // Method not allowed.
	return
}

if (request.contentType != 'application/json') {
	log "Invalid content-type ${request.contentType}"
	response.status = 415  // Unsupported type
	out.println "Accept: application/json"
	return
}

String body = request.inputStream.text
if (!body) {
	log "Empty body"
	response.status = 400 // Bad request
	return
}

// Check the signature.
String expected = headers['X-Hub-Signature']
if (!expected) {
	out.println "Missing signature."
	response.status = 400
	return
}
String signature = sign(body)
if (!MessageDigest.isEqual(signature.bytes, expected.bytes)) {
	out.println "Invalid signature."
	response.status = 403
	return
}

def push = new JsonSlurper().parseText(body)

if (push.ref != 'refs/heads/master') {
	log("Pushed to non-master branch ${push.ref}")
	response.status = 200
	return
}

// These are the repositories we are interested in.
Map repos = [ 
	groovlets:'groovlets',
	'jetstream-scripts':'scripts',
	'service-manager-installation':'manager'
]

String name = push.repository.name
String repo = repos[name]
if (repo) {
	StringBuilder buffer = new StringBuilder()
	buffer << "Push to repository ${name}\n"
	buffer << "/var/lib/downloads/scripts/pull.sh $repo".execute().text
	log buffer.toString()
}
else {
	log("Pushed to unsupported repository $name")
}
response.status = 200
return

