import groovy.json.*

String method = request.method

void log(String message) {
	String timestamp = new Date().format('yyyy-MM-dd hh:mm:ss')
	new File('/var/log/groovlets.log').append("[$timestamp] $message\n")
}

if (method != 'POST') {
	log("Invalid request method.")
	response.status = 405
	return
}
if (request.contentType != 'application/json') {
	log "Invalid content-type ${request.contentType}"
	response.status = 415
	out.println "Accept: application/json"
	return
}

String body = request.inputStream.text
if (!body) {
	log "Empty body"
	response.status = 400
	return
}

def push = new JsonSlurper().parseText(body)

if (push.ref != 'refs/heads/master') {
	log("Pushed to ${push.ref}")
	response.status = 200
	return
}

Map repos = [ groovlets:'groovlets',
	'jetstream-scripts':'scripts',
	'service-manager-installation':'manager'
]
String name = push.repository.name
String repo = repos[name]
if (repo) {
	StringBuilder buffer = new StringBuilder()
	buffer << "Push to repository $name"
	buffer << "/var/lib/downloads/scripts/pull.sh $repo".execute().text
	log buffer.toString()
}
else {
	log("Pushed to unsupported repository $name")
}
response.status = 200
