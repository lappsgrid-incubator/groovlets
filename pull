import groovy.json.*

String method = request.method

void log(String message) {
	String timestamp = new Date().format('yyyy-MM-dd hh:mm:ss')
	new File('/var/log/groovlets.log').append("[$timestamp] $message\n")
	//println "[$timestamp] $message"
}

if (method != 'POST') {
	log("Invalid request method.")
	response.status = 405
	response.text = "Invalid method: POST only."
	return
}

String body = request.inputStream.text
def push = new JsonSlurper().parseText(body)

if (push.ref != 'refs/heads/master') {
	log("Pushed to ${push.ref}")
	response.status = 200
	return
}

header.each { k,v ->
	log "$k = $v"
}
	
String name = push.repository.name
if (name == 'groovlets' || name == 'scripts' || name == 'manager') {
	log("Push to repository $name")
	"/var/lib/downloads/scripts/pull $name".execute().text
}
else {
	log("Pushed to unsupported repository $name")
}
response.status = 200
