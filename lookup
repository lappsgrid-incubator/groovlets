
@GrabConfig(systemClassLoader=true)
@Grab('com.h2database:h2:1.4.194')

import groovy.json.*
import groovy.sql.Sql

import javax.servlet.http.HttpServletResponse

import static javax.servlet.http.HttpServletResponse.*

LOG_PATH = System.getenv('LOG_PATH')
if (LOG_PATH == null) {
    LOG_PATH = '/tmp/groovlets.log'
}

DB_URL = System.getenv('DB_URL')
if (DB_URL == null) {
    DB_URL = "jdbc:h2:file://tmp/pid"
}

TEXT='text/plain'
JSON='application/json'

// Not an efficient way of performing logging, but this should be called exactly once
// per method invocation.
void log(String message) {
    String timestamp = new Date().format('yyyy-MM-dd hh:mm:ss')
    new File(LOG_PATH).append("[$timestamp] PID ${request.remoteHost} $message\n")
}

String method = request.method
if (request.method == 'OPTIONS') {
    response.status = 200
    response.addHeader('Allow', 'GET')
    response.addHeader('Accept', 'application/json')
    return
}
if (method != 'GET') {
    response.status = HttpServletResponse.SC_METHOD_NOT_ALLOWED
    response.addHeader('Allow', 'OPTIONS, GET')
    return
}

Sql sql = Sql.newInstance(DB_URL, "pid", "pid", "org.h2.Driver")
String pid
if (params.url) {
    def row = sql.firstRow("select * from pid where url = ${params.url}")
    if (row) {
        pid = row.pid
    }
    else {
        pid = UUID.randomUUID()
        sql.executeInsert('insert into pid (url,pid) VALUES (:url,:pid);', [url:params.url, pid:pid])
    }
    response.status = 200
    out.print pid
    return
}

if (params.pid) {
    def row = sql.firstRow("select * from pid where pid = ${params.pid}")
    if (row) {
        response.status = 200
        out.print row.url
        return
    }
    out.println "No such pid"
}
response.status = 400

