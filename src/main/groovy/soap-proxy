package src.main.groovy

import groovy.json.JsonBuilder
import groovy.util.logging.Slf4j

@GrabConfig(systemClassLoader=true)
@Grab('org.lappsgrid:client:2.0.4')
import org.lappsgrid.client.*

import groovy.json.JsonSlurper
@Grab('ch.qos.logback:logback-classic:1.2.3')
@Grab('org.slf4j:slf4j-simple:1.7.25')
import org.slf4j.Logger

import javax.servlet.http.HttpServletResponse

log("Servicing request.")
if (request.method == 'OPTIONS') {
    response.status = 200
    response.addHeader('Allow', 'POST')
    response.addHeader('Accept', 'application/json')
    return
}

if (request.method != 'POST') {
    response.status = HttpServletResponse.SC_METHOD_NOT_ALLOWED
    response.addHeader('Allow', 'POST')
    warn "Not a POST"
    return
}

String text = request.inputStream.text
if (request.contentType == 'text/plain') {
    Map data = [
            discriminator: 'http://vocab.lappsgrid.org/ns/media/text',
            payload: text
    ]
    text = new JsonBuilder(data).toPrettyString()
}
else if (request.contentType != 'application/json') {
    response.status = HttpServletResponse.SC_UNSUPPORTED_MEDIA_TYPE
    response.addHeader('Accept', 'application/json')
    warn "Unsupported media type ${request.contentType}"
    return
}

def parser = new JsonSlurper()
Map input = parser.parseText(text)
String username = 'tester'
String password = 'tester'

// If the proxy is specified in the header then all the parameters should be
// specifiedin headers and the input stream should just contain the JSON for
// the Data object.  Otherwise the incoming JSON object should contain all
// parameters with the Data object stored in a 'data' field.
String url = headers['LAPPS-PROXY-URL']
if (url == null) {
    if (input.parameters == null) {
        response.status = 400
        out.println "No LAPPS-PROXY header and no parameters found in the LIF object."
        return
    }
    if (input.parameters.url == null) {
        response.status = HttpServletResponse.SC_BAD_REQUEST
        out.println "No service URL provided."
        error "No service URL provided."
        return
    }
    log "Using parameters specified in the JSON"
    url = input.parameters.url
    username = input.parameters.username ?: username
    password = input.parameters.password ?: password
//    data = new JsonBuilder(input.data).toString()
}
else {
    log "Using parametres from the HTTP headers."
    username = headers['LAPPS-PROXY-USERNAME'] ?: username
    password = headers['LAPPS-PROXY-PASSWORD'] ?: password
}
log "Proxying ${url}"
ServiceClient client = new ServiceClient(url, username, password)
String json = client.execute(text)
response.status = 200
response.contentType = 'application/json'
out.println groovy.json.JsonOutput.prettyPrint(json)
log "Request serviced."
return

void debug(String message) {
    Log.logger.debug(message)
}
void log(String message) {
    Log.logger.info(message)
}
void warn(String message) {
    Log.logger.warn(message)
}
void error(String message) {
    Log.logger.error(message)
}
@Slf4j('log')
class Log {
    static Logger getLogger() {
        return log
    }
}
