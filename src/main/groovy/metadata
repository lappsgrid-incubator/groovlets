package src.main.groovy

@GrabConfig(systemClassLoader=true)
@Grab('org.lappsgrid:client:2.0.4')
import org.lappsgrid.client.*

@Grab('org.lappsgrid:serialization:2.4.0')
import org.lappsgrid.serialization.Serializer

import javax.servlet.http.HttpServletResponse

String brandeis = 'http://eldrad.cs-i.brandeis.edu:8080/service_manager'
String vassar = 'http://vassar.lappsgrid.org'

// Only accept GET requests.
String method = request.method
if (method != 'GET') {
    response.status = HttpServletResponse.SC_METHOD_NOT_ALLOWED
    response.addHeader('Allow', 'OPTIONS, GET')
    out.println "Method not allowed. Only OPTIONS or GET permitted."
    return
}

// The id parameter is required.
String id = params.id
if (!id) {
    response.status = 400
    out.println "Missing ID value."
    return
}

// Derive the service URL from the ID.
String url = "$brandeis/invoker/$id"
if (id.startsWith('anc:')) {
    url = "$vassar/invoker/$id"
}


def client = new ServiceClient(url, 'tester', 'tester')
String json = client.getMetadata();
if (!headers.Accept || headers.Accept.contains('application/json')) {
    response.status = 200
    response.contentType = 'application/json'
    out.println groovy.json.JsonOutput.prettyPrint(json)
    return
}
else if (headers.Accept?.contains('text/html')) {
    def data = Serializer.parse(json, HashMap)
    // TODO should check for errors here.
    def binding = new Binding()
    binding.url = url
    binding.html = html
    binding.payload = data.payload
    def script = new GroovyShell(binding).parse(new File('src/main/templates/metadata.gsp'))
    script.run()
}
else {
    response.status = 406
    response.contentType = 'text/plain'
    response.addHeader('Accept', 'application/json, text/html')
    out.println "Please request application/json or text/html"
}
return
